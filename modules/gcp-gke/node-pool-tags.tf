###
# This file contains no business logic.
# It's sole goal is to provide a way to read out the tags of the node pool, which include the ones we apply with this component + the ones Generated by Google.
###

locals {
  # includes the by GCP-managed tags
  all_primary_node_pool_tags = sort(data.google_compute_instance.exemplar_node_pool_instance.tags)

  exemplar_node_pool_group_url = var.skip_create_built_in_node_pool ? values(module.node_pools)[0].managed_instance_group_urls : google_container_node_pool.primary_node_pool[0].managed_instance_group_urls
}

data "google_compute_instance_group" "exemplar_node_pool_instance_group" {
  project = var.google_project

  depends_on = [google_container_node_pool.primary_node_pool[0]]
  self_link  = element(local.exemplar_node_pool_group_url, 0)
}

data "google_compute_instance" "exemplar_node_pool_instance" {
  project = var.google_project

  self_link = tolist(data.google_compute_instance_group.exemplar_node_pool_instance_group.instances)[0]
}
